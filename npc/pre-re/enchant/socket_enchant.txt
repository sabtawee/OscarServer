//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอสักครู่ !! -";
		mes "- ขณะนี้คุณกำลังแบก -";
		mes "- ไอเท็มมากเกินไป -";
		mes "- โปรดนำไอเท็มไปฝากคลัง -";
		mes "- แล้วกลับมาลองอีกครั้ง -";
		close;
	}
	mes "[Seiyablem]";
	mes "ฉันคือวิศวกรที่สามารถเพิ่ม Slot ให้กับอาวุธและชุดเกราะคุณได้.";
	mes "ฉันทำไห้ไม่ฟรีหรอกนะ และอีกอย่างใช่ว่าจะสำเร็จเสมอไป มันก็ยังมีสิทธิ์ล้มเหลวได้.";
	next;
	mes "[Seiyablem]";
	mes "มันอาจจะดูเหมือนง่ายนะ แต่ที่จริงแล้วมันมีความซับซ้อนมาก.";
	mes "ถ้าคุณสนใจในบริการของเรา ก็ไปลุยกันได้เลย.";
	next;
	switch( select( "~ เพิ่ม Slot ให้กับอาวุธ","~ เพิ่ม Slot ให้กับชุดเกราะ","~ ข้อมูลการเพิ่ม Slot","~ ยกเลิกสนทนา" ))
	{
		case 1:
			mes "[Seiyablem]";
			mes "คุณต้องการเพิ่ม slot ให้กับอาวุธของคุณงั้นหรือ?";
			mes "ฉันมีให้เลือกหลายประเภทตามระดับความยาก, ระดับ C ไม่ยากเท่าไหร่แต่ระดับ S เรียกได้ว่ายากเลยทีเดียว.";
			mes "คุณต้องการให้ฉันเพิ่ม slot ในระดับไหน?";
			next;
			switch( select( "~ ระดับ C","~ ระดับ B","~ ระดับ A","~ ระดับ S" ))
			{
				case 1:
					mes "[Seiyablem]";
					mes "ระดับ C งั้นหรอ?";
					mes "ตอนนี้ ฉันสามารถทำให้คุณได้ แต่มันมีหลากหลายรายการ กรุณาเลือกมา 1 รายการจากอาวุธที่ฉันสามารถเพิ่ม slot ให้ได้.";
					next;
					switch(select("~ Trident:~ Rope:~ Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "ระดับ B หรอ?";
					mes "งั้นหรอ เอาแบบนี้ใช่ป่ะ มันเสี่ยงกว่าระดับที่ผ่านมานะ.";
					mes "กรุณาเลือก อาวุธที่คุณจะให้เราเพิ่ม Slot ให้ 1 ชิ้น.";
					next;
					switch(select("~ Chain:~ Gladius:~ Gakkung Bow:~ Pike:~ Haedonggum:~ Lute:~ Wire:~ Waghnakh:~ Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "โอ้วววว..ระดับ A.";
					mes "มันมีความเสี่ยงสูงมากนะ!";
					mes "ถ้าคุณพร้อมที่จะเสี่ยง ก็เลือกอาวุธที่คุณจะเพิ่ม Slot มา 1 รายการ";
					next;
					switch(select("~ Hunter Bow:~ Survivor's Rod(INT):~ Zweihander:~ Flamberge:~ Infiltrator:~ Ballista:~ Stunner:~ Berserk:~ Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "ว้าาาาาาาาาาา";
					mes "ระดับ S งั้นหรอ!!!";
					mes "มันคงเป็นเรื่องมหัศจรรย์มาก ถ้าหากมันทำสำเร็จ.";
					mes "โอเค...ถ้าคุณพร้อม ก็เลือกรายการอาวุธมา 1 รายการ";
					next;
					switch(select("~ Gungnir:~ Poison Knife:~ Ice Pick:~ Sucsamad:~ Ginnungagap:~ Cutlas:~ Crescent Scythe:~ Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "คุณเลือกที่จะเพิ่ม Slot ให้กับชุดเกราะงั้นหรือ";
			mes "แน่นอน ไม่มีปัญหา.";
			mes "ชุดเกราะที่จะให้เพิ่ม Slot ให้ มันมี 4 ระดับตามความยากนะ";
			mes "ระดับ C ไม่ยากเท่าไหร่แต่ระดับ S ยากพอสมควร.";
			mes "คุณต้องการจะเพิ่ม Slot ที่ระดับไหน";
			next;
			switch( select( "~ ระดับ C","~ ระดับ B","~ ระดับ A","~ ระดับ S" ))
			{
				case 1:
					mes "[Seiyablem]";
					mes "ชุดเกราะระดับ C...ไม่มีปัญหา";
					mes "มันคงดูเหมาะสม.";
					mes "เลือกชุดเกราะที่คุณต้องการจะเพิ่ม Slot";
					next;
					switch(select("~ Mantle:~ Coat:~ Circlet:~ Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "คุณเลือกที่ความเสี่ยงระดับ B งั้นหรอ.";
					mes "ได้.แล้วคุณต้องการเพิ่ม Slot ชิ้นไหนละ";
					next;
					switch(select("~ Mirror Shield:~ Chain Mail:~ Saint Robe:~ Silk Robe:~ Boots:~ Shoes:~ Muffler:~ Guard:~ Buckler:~ Shield:~ Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "โอ้โห...ระดับ A";
					mes "ความเสี่ยงมันสูงอยู่นะ!";
					mes "เอาเถอะ เลือกชุดเกราะที่คุณต้องการจะเพิ่ม Slot มาเลย";
					next;
					switch(select("~ Gemmed Sallet:~ Bucket Hat:~ Memory Book:~ Tights:~ Legion Plate Armor:~ Full Plate:~ Thief Clothes:~ Greaves:~ Coif:~ Manteau:~ Helm:~ Ninja Suit:~ Orc Helm:~ Ancient Cape:~ Monk Hat:~ Golden Gear:~ Brooch:~ Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "อืม...ระดับ S";
					mes "คุณแน่ใจแล้วหรือที่จะเลือกความเสี่ยงในระดับนี้";
					mes "เอาเถอะนะ ฉันจะพยามทำมันให้ดีที่สุด เลือกชุดเกราะที่คุณต้องการมา 1 ชิ้น";
					next;
					switch(select("~ Majestic Goat:~ Spiky Band:~ Bone Helm:~ Corsair:~ Crown:~ Tiara:~ Sphinx Hat:~ Robe of Cast:~ Earring:~ Ring:~ Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "เยี่ยม.อันที่จริงฉันไม่ค่อยมีศิลปะ ในการเพิ่ม Slot สักเท่าไหร่";
			mes "มันเป็นกระบวนการที่ซับซ้อน และส่งผลกระทบมากมาย.";
			mes "ฉันจะบอกอะไรให้นะ";
			next;
			mes "[Seiyablem]";
			mes "ในบางเมือง, มันจะมีความสำเร็จตามระดับของอุปกรณ์ ที่มีความยากง่ายในการหา.";
			mes "ในเมืองอื่นๆ มักจะทำตรงข้ามกับความเป็นจริง.";
			mes "มันไม่ได้มีความพิเศษอะไรเลย ไอการเพิ่ม Slot เนี่ย";
			next;
			mes "[Seiyablem]";
			mes "โอ้ว..เกือบลืม ยังมีสิ่งสำคัญอีกสิ่งทีคุณควรจะรู้.";
			mes "ถ้าคุณให้ฉันเพิ่ม Slot ในอุปกรณ์บางอย่าง, คุณต้องเช๊คให้แน่ใจว่าคุณไม่ได้สวมใส่มันอยู่.";
			next;
			mes "[Seiyablem]";
			mes "นี่คือตัวอย่าง:ถ้าคุณมี +7 Manteau และ Manteau ที่แสนจะธรรมดา อยู่ในช่องสวมใส่ของคุณ, ฉันจะสุ่มเลือกใน 2 ชิ้นนี้มาทำการเพิ่ม Slot.";
			mes "ฉะนั้น จำไว้ ระวังให้ดี ถ้าฉันสุ่มเอา +7 ของคุณมา อย่ามาโทษฉันละกัน เพราะฉันเตือนแล้ว.";
			next;
			mes "[Seiyablem]";
			mes "อีกรอบนะ, ^FF0000โปรดระวังอุปกรณ์ที่คุณต้องการให้เพิ่ม Slot ดูให้แน่ใจว่า ถอดแล้วหรือยัง^000000.";
			mes "อุปกรณ์ที่มีชื่อเดียวกันกับสิ่งที่คุณจะเพิ่ม Slot ควรเก็บของเหล่านั้นไว้ในช่องเก็บของใน Kafra, เก็บมันซะ";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "มันใช้ไม่ยากหรอกนะ.";
			mes "ถ้าเมื่อไหร่ที่คุณต้องการที่จะให้ฉันเพิ่ม Slot ให้ละก็, ก็กลับมาหาฉันนี่...เข้าใจไหม?.";
			mes "บ๊าบ บาย.....";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Seiyablem]";
	mes "คุณต้องการให้ฉันเพิ่ม Slot ใน "+ getitemname(.@item_id) +"?";
	mes "โปรดนำ สิ่งที่ฉันต้องการเหล่านี้ ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "มาให้ฉัน และเงินอีกจำนวน ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 ในการใช้บริการ.";
	mes "อ่า,และอย่าลืมนำสิ่งนี้มาด้วย "+ getitemname(.@item_id) +"!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(.@item_id,5) & EQP_HAND_R) // weapon
	{
		mes "[Seiyablem]";
		mes "ฉันจะพยายามเพิ่ม Slot ให้ หากคุณจำเป็น แต่คุณต้องมี Item ชิ้นนั้นมาและเงินอีกจำนวนหนึ่ง.";
		mes "อย่างไรก็ตาม, คุณควรจะรู้ไว้ว่าหากคุณไม่นำสิ่งที่ฉันต้องการมา ฉันก็จะไม่ทำการอัพเกรดอุปกรณ์ให้คุณ.";
		mes "ดังนั้น, ฉันจึงอยากจะเตือนคุณด้วยความยุธติธรรม";
		next;
		mes "[Seiyablem]";
		mes "หากการเพิ่ม Slot ในครั้งนี้ล้มเหลว, แล้ว ^FF0000อาวุธ^000000, ^FF0000และการ์ดทีใส่อยู่ข้างในจะถูกทำลายไปพร้อมกัน^000000.";
	}
	else // armor
	{
		mes "[Seiyablem]";
		mes "ถ้าคุณมีวัสดุที่ฉันต้องการ, และเงินในค่าแรงของฉัน และชุดเกราะ, ผมก็พร้อมที่จะเพิ่ม Slot ให้กับคุณ.";
		mes "แต่ก่อนที่, ฉันต้องขอเตือนคุณก่อนเรื่องของความเสี่ยงในการเพิ่ม Slot.";
		next;
		mes "[Seiyablem]";
		mes "ถ้าหากการเพิ่ม Slot ครั้งนี้ล้มเหลว, แล้ว ^FF0000ชุดเกราะ^000000, ^FF0000ที่มันอัพเกรดมา^000000 ^FF0000และการ์ดที่ใส่อยู่ จะถูกทำลายไปพร้อมกัน^000000.";
	}
	mes "คุณยังต้องการที่จะเพิ่ม Slot หรือไม่?";
	next;
	switch(select("~ ฉันพร้อมที่จะเพิ่ม Slot แล้ว:~ ยกเลิกสนทนา"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "เอาเถอะ!เรามาเริ่มกันดีกว่า";
				mes "คุณภาวนาให้มันสำเร็จเถอะนะ.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "เยี่ยม, มันสำเร็จและผ่านไปด้วยดี";
					mes "ยินดีด้วยกับความสำเร็จครั้งนี้ ! ";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "แล้วพบกันใหม่..อีกครั้งเพื่อน!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "โอ้ว...ขอโทษที มันไม่สำเร็จ.";
					mes "อย่างไรก็ตาม, ฉันจะทำตัวเป็นเหมือนไม่รู้ไม่เห็นอะไรเลยละกัน.";
					mes "นี่คือโชคชะตาของคุณ, และเป็นสิ่งที่พระท่านกำหนดมาแล้ว, เข้าใจไหม?";
					mes "ครั้งหน้าเรามาพยายามกันใหม่นะ.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Seiyablem]";
					mes "ฉันหวังว่า รอบหน้า คุณจะโชคดีนะ";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "ฉันต้องการสมาธิในการเพิ่ม Slot, แต่คุณไม่ควรพลาดสิ่งต่างๆ.";
				mes "คุณต้องแน่ใจว่าคุณมี Item ที่จะให้เพิม, มีแร่และวัสดุครบ สุดท้ายก็ค่าแรงของฉันครบ";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "บางทีคุณอาจจะต้องการเวลาในการคิดใช่ไหมละ, หืม?";
			mes "เอาเถอะ..ฉันเข้าใจ.";
			mes "มันอาจะดูเหมือนสนุกแต่มันไม่ปลอดภัยใช่ไหมละ";
			close;
	}
}

payon,28,119,5	duplicate(SocketEnchant)	Seiyablem#pay	84
//lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc_in,60,61,6	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "เจาะรูในไอเท็มสวมใส่";
	mes "อาจทำให้คุณสมบัติไอเท็มใส่ด้วย";
	next;
	mes "[Young Man]";
	mes "Ohมันเป็นเฉพาะสิ่งที่พิเศษเท่านั้น";
	mes "แต่บางที คุณสามารถเจาะรูได้";
	mes "2 Slots ในไอเท็มชิ้นเดียวกัน";
	mes "แน่นอน ไม่ใช่ว่าจะทำได้ทุก ๆ ชิ้นหรอกนะ";
	next;
	mes "[Young Man]";
	mes "ฉันคิดจะเรียนรู้อย่่างจริงจัง";
	mes "และกลายเป็นช่างเจาะรู";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,24,119,5	duplicate(SocketMan)	Young Man#pay	97
morocc_in,63,71,5	duplicate(SocketMan)	Young Man#moc	97
//lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
