//===== rAthena Script =======================================
//= Illusion of Abyss
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

//============================================================
//= mapflag
//============================================================
tur_d03_i	mapflag	nobranch
tur_d03_i	mapflag	nomemo

tur_d04_i	mapflag	nobranch
tur_d04_i	mapflag	nomemo

//============================================================
//= warp
//============================================================
tur_d03_i,218,70,0	warp	tur_d03_i-tur_d04_i	1,1,tur_d04_i,100,189
tur_d04_i,100,192,0	warp	tur_d04_i-tur_d03_i	1,1,tur_d03_i,216,74

alberta,212,30,5	script	Cat#sfids	4_CAT_SAILOR4,{}
alberta,211,28,5	script	Knight Aylvar#ill_abyss	4_M_YOUNGKNIGHT,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" เข้าสู้ดันเจี้ยน",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1:
			mes .NPCname$;
			mes "ขอให้โชคดี.";
			close2;
			warp "tur_d03_i",131,193;
			end;
			
		case 2:
			mes .NPCname$;
			mes "แล้วพบกันใหม่.";
			close;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = "~";
		end;
}
tur_d03_i,125,191,5	script	Cat#sfids2	4_CAT_SAILOR4,{}
tur_d03_i,126,192,5	script	Knight Aylvar#ill_abyss2	4_M_YOUNGKNIGHT,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" ออกจากดันเจี้ยน",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1:
			mes .NPCname$;
			mes "ขอให้โชคดี.";
			close2;
			warp "alberta",215,28;
			end;
			
		case 2:
			mes .NPCname$;
			mes "แล้วพบกันใหม่.";
			close;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = "~";
		end;
}


//============================================================
//= Illusion Gears Trader.
//============================================================
alberta,225,28,3	script	Equipment Researcher#illusion_abyss	4_M_LGTMAN,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" อัพเกรด อาวุธ.",
		""+.sb$+" อัพเกรด ชุดเกราะ.",
		""+.sb$+" กล่องอัพเกรด."
	)){
		case 1:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "WEAPON_UP_Abyss",1;
			end;
		case 2:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "ARMOR_UP_Abyss",1;
			end;
		case 3:
			mes .NPCname$;
			mes "โปรดรอสักครู่.";
			close2;
			callshop "REFINE_Abyss",1;
			end;
	}
	end;
	
OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";
	.msg$ = "Illusion of Abyss";
	setunittitle getnpcid(0),.msg$;
	setunitdata getnpcid(0),UNPC_GROUP_ID,1014;
	end;
}
//skycity,72,90,3	duplicate(Equipment Researcher#illusion_abyss)	Equipment Researcher#illusion_abyss2	4_M_LGTMAN


//============================================================
//= Mob Spawn
//============================================================
tur_d03_i,0,0	monster	Ominous Assaulter	3799,35,5000
tur_d03_i,0,0	monster	Ominous Permeter	3800,15,5000
tur_d03_i,0,0	monster	Ominous Freezer	3801,40,5000
tur_d03_i,0,0	monster	Ominous Solider	3802,40,5000
tur_d03_i,0,0	monster	Ominous Heater	3803,10,5000

tur_d04_i,0,0	monster	Ominous Assaulter	3799,42,5000,0,"illusion_mob#abyss::OnKill"
tur_d04_i,0,0	monster	Ominous Permeter	3800,10,5000,0,"illusion_mob#abyss::OnKill"
tur_d04_i,0,0	monster	Ominous Freezer	3801,15,5000,0,"illusion_mob#abyss::OnKill"
tur_d04_i,0,0	monster	Ominous Solider	3802,10,5000,0,"illusion_mob#abyss::OnKill"
tur_d04_i,0,0	monster	Ominous Heater	3803,33,5000,0,"illusion_mob#abyss::OnKill"

-	script	illusion_mob#abyss	-1,{
	OnInit:
		// ชื่อไฟล์แผนที่.
		.mapname$ = "tur_d04_i";
		// มอนสเตอร์ MVP ID.
		.bossID = 3804;
		// จำนวนมอนสเตอร์ที่ต้องกำจัด เพื่อเรียกบอส.
		.mob_count = 1000;
		$turtle_boss = 0;		// นับจำนวนบอส หากบอสเกิดจะเป็น 1 และจำนวนมอนสเตอร์ที่ต้องกำจัด จะไม่ถูกนับเพิ่ม. (ป้องกันการสแปมบอส)
								// ถ้าบอสตายจะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้ง.
		$illusion_abyss = 0;	//จะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้งเมื่อรีสคริป.
		// บอสมอนสเตอร์มีโอกาสเกิดแม้จะกำจัดมอนอื่นๆไม่ครบ
		.lucky_chance = false;	// true = เปิด | false = ปิด
		.max_chance = 10000;	// 100.00%
		.chance = 500;			// 5.00%
		// แสดงการนับจำนวนมอนที่กำจัด
		.show_count = true;	// true = เปิด | false = ปิด
	end;
		
	OnKill:
		if ($turtle_boss == 1) end;
		$illusion_abyss++;
		if (.show_count) {
			dispbottom "Illusion of Abyss: บอสมอนสเตอร์จะปรากฏตัวเมื่อกำจัดมอนสเตอร์ครบ ["+callfunc("F_InsertComma",$illusion_abyss)+"/"+callfunc("F_InsertComma",.mob_count)+"] ตัว.";
		}
		if ($illusion_abyss >= .mob_count && $turtle_boss == 0) {
			donpcevent strnpcinfo(0)+"::OnSummonMVP";
		}
		if (.lucky_chance) {
			if ($illusion_abyss <= .mob_count) {
				if (rand(.max_chance) <= .chance) {
					donpcevent strnpcinfo(0)+"::OnSummonMVP";
				}
			}
		}
	end;
		
	OnSummonMVP:
		$turtle_boss++;
		if (.lucky_chance) {
			mapannounce .mapname$,""+getmonsterinfo(.bossID,MOB_NAME)+":  ใครกันที่กล้ามารบกวนการหลับไหลของข้า!!",bc_map,0xFFBF5C,FW_BOLD,20;
			sleep 5000;
		}
		mapannounce .mapname$,"Illusion of Abyss: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" จะปรากฏตัวในอีก 10 วินาที.",bc_map,0xFFBF5C,FW_BOLD,20;		
		sleep 10000;
		mapannounce .mapname$,"Illusion of Abyss: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" ได้ปรากฏตัวขึ้นแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		areamonster .mapname$,93,97,106,78,"--ja--",.bossID,1,strnpcinfo(0)+"::OnMVPDead";	
		$illusion_abyss = 0;
		end;	
	
	OnMVPDead:
		$turtle_boss--;
		mapannounce .mapname$,"Illusion of Abyss: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[0],MOB_NAME)+" ได้ถูกกำจัดลงแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		end;
}

//============================================================
//= Daily Quest.
//============================================================
tur_d03_i,235,187,7	script	Soldier#ill_abyss1	4_M_CRU,{
	disable_items;
	if (checkquest(.questid[1],PLAYTIME) == 2) { erasequest .questid[0]; erasequest .questid[1]; }
	mes .NPCname$;
	mes "เข้ามาเลยเหล่านักผจญภัย";
	mes "ข้ามีเควสประจำวันให้พวกเจ้าทำ";
	mes "สนใจเควสไหนก็รับไปได้เลย.";
	next;
	switch(select(
		"[^009900"+.sb$+"^000000] ความปรารถนาที่ว่างเปล่า",
		"[^FF0000"+.sb$+"^000000] ยกเลิก"
	)) {
		case 1:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[0]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "และรวบรวม <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM> x "+.qresource[1]+" อัน";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[0];
				close;
			}
			else if (isbegin_quest(.questid[0]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[0],HUNTING) == 2) && (countitem(.qresource[0]) >= .qresource[1]) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					delitem .qresource[0],.qresource[1];
					getexp .rewardexp[0],.rewardexp[1];
					getitem .reward[0],.reward[1];
					completequest .questid[0];
					setquest .questid[1];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "และรวบรวม <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM> x "+.qresource[1]+" อัน";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 2: end;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
		//waitingroom "เควสประจำวัน",0;
		//----------------------------------------
		// base level request.
		//----------------------------------------
		.lvlrequest = 150;
		//----------------------------------------
		// quest reward.
		//----------------------------------------
		setarray .reward, 
			25271, 3;			//Illusion Stone 3 ea.
		setarray .rewardexp,
			4000000, 2000000;	//BEXP ,JEXP for quest id 2000057
		//----------------------------------------
		// quest id.
		//----------------------------------------
		setarray .questid,
			2000057,	//Empty Desire 
			2000058;	//Empty Desire  - Waiting
		//----------------------------------------
		// quest request.
		//----------------------------------------
		setarray .qresource,
			25316, 5;	//Old Metal Pieces
		//----------------------------------------
		// moster hunt.
		//----------------------------------------
		setarray .qhunt,
			3799, 10;	//Ominous Assaulter
		//----------------------------------------
		// quest mark.
		//----------------------------------------
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000057) && BaseLevel >= 150";
		end;
}

tur_d04_i,97,112,9	script	Fragile Woman#ill_abyss	16,{
	disable_items;
	if (checkquest(.questid[1],PLAYTIME) == 2) { erasequest .questid[0]; erasequest .questid[1]; }
	if (checkquest(.questid[2],PLAYTIME) == 2) { erasequest .questid[2]; erasequest .questid[3]; }
	mes .NPCname$;
	mes "เข้ามาเลยเหล่านักผจญภัย";
	mes "ข้ามีเควสประจำวันให้พวกเจ้าทำ";
	mes "สนใจเควสไหนก็รับไปได้เลย.";
	next;
	switch(select(
		"[^009900"+.sb$+"^000000] ความหิวไม่รู้จบ (1)",
		"[^009900"+.sb$+"^000000] ความหิวไม่รู้จบ (2)",
		"[^FF0000"+.sb$+"^000000] ยกเลิก"
	)) {
		case 1:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[0]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[2],MOB_NAME)+"^000000 x "+.qhunt[3]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[4],MOB_NAME)+"^000000 x "+.qhunt[5]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[0];
				close;
			}
			else if (isbegin_quest(.questid[0]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[0],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[0],.rewardexp[1];
					getitem .reward[0],.reward[1];
					completequest .questid[0];
					setquest .questid[1];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 2:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[2]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[6],MOB_NAME)+"^000000 x "+.qhunt[7]+" ตัว";
				mes "และรวบรวม <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM> x "+.qresource[1]+" อัน";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[2];
				close;
			}
			else if (isbegin_quest(.questid[2]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[0],HUNTING) == 2) && (countitem(.qresource[0]) >= .qresource[1]) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					delitem .qresource[0],.qresource[1];
					getexp .rewardexp[2],.rewardexp[3];
					getitem .reward[2],.reward[3];
					completequest .questid[2];
					setquest .questid[3];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 3: end;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
		//waitingroom "เควสประจำวัน",0;
		//----------------------------------------
		// base level request.
		//----------------------------------------
		.lvlrequest = 150;
		//----------------------------------------
		// quest reward.
		//----------------------------------------
		setarray .reward, 
			25271, 3,			//Illusion Stone 3 ea.
			25271, 2;			//Illusion Stone 2 ea.			
		setarray .rewardexp,
			4000000, 2000000,	//BEXP ,JEXP for quest id 2000059
			2000000, 1000000;	//BEXP ,JEXP for quest id 2000061
		//----------------------------------------
		// quest id.
		//----------------------------------------
		setarray .questid,
			2000059,	//Endless Hunger 1 
			2000060,	//Endless Hunger 1 - Waiting
			2000061,	//Endless Hunger 2 
			2000062;	//Endless Hunger 2 - Waiting
		//----------------------------------------
		// quest request.
		//----------------------------------------
		setarray .qresource,
			7564, 10;	//Rotten Meat
		//----------------------------------------
		// moster hunt.
		//----------------------------------------
		setarray .qhunt,
			3800, 10,	//Ominous Permeter
			3801, 10,	//Ominous Freezer
			3802, 10,	//Ominous Solider
			3803, 10;	//Ominous Heater
		//----------------------------------------
		// quest mark.
		//----------------------------------------
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000059) && BaseLevel >= 150";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000061) && BaseLevel >= 150";
		end;
}