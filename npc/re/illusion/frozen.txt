//===== rAthena Script =======================================
//= Illusion of Frozen
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

//============================================================
// mapflag
//============================================================
ice_d03_i	mapflag	nobranch
ice_d03_i	mapflag	nomemo

//============================================================
// Warp
//============================================================
ice_dun02,150,11,0	script	Entrance#illusion_frozen	4_PURPLE_WARP,{
	if(select("เข้าสู่ดันเจี้ยน:ยกเลิก") == 2){ end; }
	warp "ice_d03_i",149,25;
	end;
}
ice_d03_i,149,21,0	script	Entrance#illusion_frozen_2	4_PURPLE_WARP,{
	if(select("ออกจากดันเจี้ยน:ยกเลิก") == 2){ end; }
	warp "ice_dun02",150,16;
	end;
}

//============================================================
// Illusion Gears Trader.
//============================================================
ice_dun02,152,18,3	script	Illusion Researcher#illusion_frozen	4_M_ALCHE_B,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" อัพเกรด อาวุธ.",
		""+.sb$+" อัพเกรด ชุดเกราะ.",		
		""+.sb$+" กล่องอัพเกรด."
	)){
		case 1:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "WEAPON_UP_Frozen",1;
			end;
		case 2:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "ARMOR_UP_Frozen",1;
			end;
		case 3:
			mes .NPCname$;
			mes "โปรดรอสักครู่.";
			close2;
			callshop "REFINE_Frozen",1;
			end;
	}
	end;
	
OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";
	.msg$ = "Illusion of Frozen";
	setunittitle getnpcid(0),.msg$;
	setunitdata getnpcid(0),UNPC_GROUP_ID,1014;
	end;
}
//skycity,66,90,3	duplicate(Illusion Researcher#illusion_frozen)	Illusion Researcher#illusion_frozen2	4_M_ALCHE_B

//============================================================
// Mob Spawn.
//============================================================
ice_d03_i,0,0	monster	Furious Gazeti	3792,30,5000,0,"illusion_mob#frozen::OnKill"
ice_d03_i,0,0	monster	Furious Snowier	3793,30,5000,0,"illusion_mob#frozen::OnKill"
ice_d03_i,0,0	monster	Furious Ice Titan	3794,20,5000,0,"illusion_mob#frozen::OnKill"
ice_d03_i,0,0	monster	Solid Icicle	3795,20,5000,0,"illusion_mob#frozen::OnKill"

-	script	illusion_mob#frozen	-1,{
	OnInit:
		// ชื่อไฟล์แผนที่.
		.mapname$ = "ice_d03_i";
		.mob_count = 1500;		// จำนวนมอนสเตอร์ที่ต้องกำจัด เพื่อเรียกบอส.
		$illusion_frozen = 0;	//จะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้งเมื่อรีสคริป.
		$c_seal = 0;
		// บอสมอนสเตอร์มีโอกาสเกิดแม้จะกำจัดมอนอื่นๆไม่ครบ
		.lucky_chance = false;	// true = เปิด | false = ปิด
		.max_chance = 10000;	// 100.00%
		.chance = 500;			// 5.00%
		// แสดงการนับจำนวนมอนที่กำจัด
		.show_count = true;	// true = เปิด | false = ปิด
	end;
		
	OnKill:
		if ($c_seal == 1) end;
		$illusion_frozen++;
		if (.show_count) {
			dispbottom "Illusion of Frozen: Crystal Seal จะปรากฏเมื่อกำจัดมอนสเตอร์ครบ ["+callfunc("F_InsertComma",$illusion_frozen)+"/"+callfunc("F_InsertComma",.mob_count)+"] ตัว.";
		}
		if ($illusion_frozen >= .mob_count && $c_seal == 0) {
			donpcevent strnpcinfo(0)+"::OnSummonSeal";
		}
		if (.lucky_chance) {
			if ($illusion_frozen <= .mob_count) {
				if (rand(.max_chance) <= .chance) {
					donpcevent strnpcinfo(0)+"::OnSummonSeal";
				}
			}
		}
	end;
		
	OnSummonSeal:
		$c_seal++;
		sleep 1000;
		enablenpc "Crystal Seal#1";
		enablenpc "Crystal Seal#2";
		enablenpc "Crystal Seal#3";
		enablenpc "Crystal Seal#4";		
		mapannounce .mapname$,"Illusion of Frozen: Crystal Seal ได้ปรากฏขึ้นแล้ว!!",bc_map,0xFFBF5C,FW_BOLD,20;
		$illusion_frozen = 0;
		end;
}

-	script	Crystal Seal	-1,{
	OnTouch:
		progressbar_npc "green",1,strnpcinfo(0);
		$crystal_seal++;
		specialeffect 16;
		specialeffect 72;
		mapannounce .mapname$,"Illusion of Frozen: "+$crystal_seal+" Crystal Seal ทำงาน!!",bc_map,0xFFBF5C,FW_BOLD,20;
		sleep 2000;
		disablenpc strnpcinfo(3);
	if($crystal_seal >= 4){
		mapannounce .mapname$,"Illusion of Frozen: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" จะปรากฏตัวในอีก 10 วินาที.",bc_map,0xFFBF5C,FW_BOLD,20;		
		sleep 10000;
		donpcevent "frozen_boss_spawn::OnSummonMVP";
	}
	end;
	
	OnInit:
		.mapname$ = "ice_d03_i";
		$crystal_seal = 0;
		.bossID = 3796;
		disablenpc "Crystal Seal#1";
		disablenpc "Crystal Seal#2";
		disablenpc "Crystal Seal#3";
		disablenpc "Crystal Seal#4";
	end;
}
ice_d03_i,126,172,4	duplicate(Crystal Seal)	Crystal Seal#1	111
ice_d03_i,126,126,4	duplicate(Crystal Seal)	Crystal Seal#2	111
ice_d03_i,172,172,4	duplicate(Crystal Seal)	Crystal Seal#3	111
ice_d03_i,172,126,4	duplicate(Crystal Seal)	Crystal Seal#4	111

-	script	frozen_boss_spawn	-1,{
	OnSummonMVP:
		mapannounce .mapname$,"Illusion of Frozen: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" ได้ปรากฏตัวขึ้นแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		monster .mapname$,150,135,"--ja--",.bossID,1,strnpcinfo(0)+"::OnMVPDead";	
	end;
	
	OnMVPDead:
		mapannounce .mapname$,"Illusion of Frozen: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" ได้ถูกกำจัดลงแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		$crystal_seal = 0;
		$c_seal--;
	end;
	
	OnInit:
		.mapname$ = "ice_d03_i";
		.bossID = 3796;
	end;
}


//============================================================
// Daily Quest.
//============================================================
ice_d03_i,156,40,4	script	Jays	4_M_FROZEN_GC,{
	disable_items;
	if (frozen != gettime(5)) {
		if (checkquest(15110) == 2) { erasequest 15110; }
		if (checkquest(15111) == 2) { erasequest 15111; }
		if (checkquest(15112) == 2) { erasequest 15112; }
		if (checkquest(15113) == 2) { erasequest 15113; }
		if (checkquest(15116) == 2) { erasequest 15116; }
		if (checkquest(15117) == 2) { erasequest 15117; }
		if (checkquest(15118) == 2) { erasequest 15118; }
		if (checkquest(15119) == 2) { erasequest 15119; }
		frozen = gettime(5);
		frozen_daily = 0;
		mes .NPCname$;
		mes "เควสประจำวันทั้งหมดถูกรีเซ็ตแล้ว";
		mes "โปรดคุยกับข้าใหม่อีกครั้ง.";
		close;
	}
	if (frozen_daily == 0 && checkquest(15110) == -1 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "เฮ้!! เจ้าน่ะ";
		mes "เจ้าเป็นนักผจญภัยใช่ไหม?";
		mes "มานี่...มานี่";
		mes "ช่วยข้าสองคนออกไปที.";	
		next;
		mes .NPCname$;
		mes "อะไรนะ...เจ้าจะถามอะไรข้า";
		mes "ข้าอยู่ในนี้แทบไม่ได้ยินเสียงเจ้าเลย.";
		next;
		mes .NPCname2$;
		mes "เจ้าโง่... Jays";
		mes "รีบๆให้เจ้านั่นช่วยพวกเราสักที";
		mes "ข้าจะแข็งตายอยู่แล้ว.";
		next;
		mes .NPCname$;
		mes "ข้ารู้แล้วน่าไอบ้า Brides";
		mes "เอาล่ะ เจ้านักผจญภัย";
		mes "เจ้าช่วยรวบรวม <ITEM>"+getitemname(25309)+"<INFO>25309</INFO></ITEM>";
		mes "มาก่อไฟละลายน้ำแข็งพวกนี้หน่อยได้ไหม?";
		mes "ไหนๆเจ้าก็เข้ามาที่นี่แล้ว ก็ช่วยพวกข้าหน่อยล่ะกัน.";	
		next;
		mes .NPCname2$;
		mes "เจ้าจะหามันได้ภายในดันเจี้ยน";
		mes "ลองดูตามกองทรายที่เจ้าเดินไปเจอล่ะกัน.";		
		setquest 15110;
		frozen_daily = 1;
		end;
	}	
	if (frozen_daily == 1 && checkquest(15110) == 1 && countitem(25309) >= 10 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "โอ้...ในที่สุดเจ้าก็กลับมาสักที";
		mes "ข้ารอเจ้านานมากแล้ว";
		mes "เจ้าได้รวบรวม <ITEM>"+getitemname(25309)+"<INFO>25309</INFO></ITEM> มาครบหรือเปล่า?";
		next;
		mes .NPCname2$;
		mes "เร็วๆเข้า เจ้ารีบนำ <ITEM>"+getitemname(25309)+"<INFO>25309</INFO></ITEM>";
		mes "มาจุดไฟเพื่อละลายน้ำแข็งให้พวกข้าได้แล้ว";
		mes "ข้าหนาวจะตายอยู่แล้ว.";
		next;
		mes .NPCname$;
		mes "เอาล่ะเจ้าเตรียมก่อไฟได้แล้ว";
		mes "ข้าเชื่อว่ามันจะต้องได้ผลแน่ๆ.";
		next;
		enablenpc "#DryTwings";
		mes .NPCname2$;
		mes "นั่นแหละ แบบนั้น";
		mes "จุดไฟขึ้นมาเลย";
		mes "น้ำแข็งพวกนี้จะได้ละลายไปสักที.";
		next;
		disablenpc "#DryTwings";
		enablenpc "#Bonefire";
		mes .NPCname$;
		mes "ใช่เลย แบบนั้นแหละ";
		mes "น้ำแข็งพวกนี้จะได้ละลายไปสักที";
		mes "ข้าอยากจะออกไปแล้ว.";
		next;
		mes .NPCname2$;
		mes "อ้าก!";
		mes "มันไม่ได้ผล";
		mes "ข้าว่ามันต้องใช้วิธีอื่นแล้วล่ะ.";
		disablenpc "#Bonefire";
		completequest 15110;
		frozen_daily = 2;
		delitem 25309,10;
		getexp 100000,100000;
		end;
	}	
	if (frozen_daily == 2 && checkquest(15110) == 2 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "ไฟไม่สามารถละลายน้ำแข็งนี่ไปได้เลย";
		mes "ข้าจะทำยังไงดี ข้าหนาวจะแย่อยู่แล้ว";
		mes "เจ้ามีความคิดว่ายังไงบ้าง Brides.";
		next;
		mes .NPCname2$;
		mes "ข้าคิดออกแล้ว";
		mes "เจ้านักผจญภัย ข้าจะให้เจ้าไปกำจัดพวกมอนสเตอร์รอบๆนี้";
		mes "ผิวหนังพวกมันเป็นน้ำแข็ง";
		mes "แถมยังแข็งแรงมากอีกด้วย.";
		next;
		mes .NPCname2$;
		mes "ข้าเชื่อว่าเจ้าสามารถเอาชนะพวกมันได้";
		mes "และเราจะใช้ผิวหนังของพวกมันในการทุบทำลายน้ำแข็งพวกนี้";
		mes "ข้าเชื่อว่าน้ำแข็งมันต้องแตกได้ด้วยน้ำแข็ง.";
		next;
		mes .NPCname$;
		mes "ข้าไม่รู้ว่ามันจะได้ผลไหม";
		mes "แต่ก็เอาเถอะ เจ้าช่วยลองทำตามหน่อยล่ะกัน";
		mes "ข้าหวังว่าเจ้าจะไม่โดนมอนสเตอร์พวกนั้นเล่นงานหลอกนะ.";
		frozen_daily = 3;
		.@r = rand(1,4);
		if (.@r == 1) setquest 15111;
		if (.@r == 2) setquest 15117;
		if (.@r == 3) setquest 15118;
		if (.@r == 4) setquest 15119;
		end;
	}
	if (frozen_daily == 3 && checkquest(15110) == 2 && BaseLevel >= 110) {
		if (checkquest(15111,HUNTING) == 2){
			mes .NPCname$;
			mes "โอ้ เจ้ากลับมาแล้ว";
			mes "เจ้าปลอดภัยดีสินะ";
			mes "ข้าเชื่อแล้วว่าเจ้าเก่งจริงๆ.";
			next;
			mes .NPCname2$;
			mes "เอาล่ะ ข้าจะให้เจ้าเตรียมตัว";
			mes "ที่จะทำลายน้ำแข็งพวกนี้";
			mes "เพื่อช่วยพวกข้าออกไป";
			mes "ถ้าเจ้าพร้อมเมื่อไรก็ให้คุยกับ Jays ละกัน.";
			completequest 15111;
			getexp 100000,100000;
			frozen_daily = 4;
			end;
		}
		if (checkquest(15117,HUNTING) == 2){
			mes .NPCname$;
			mes "โอ้ เจ้ากลับมาแล้ว";
			mes "เจ้าปลอดภัยดีสินะ";
			mes "ข้าเชื่อแล้วว่าเจ้าเก่งจริงๆ.";
			next;
			mes .NPCname2$;
			mes "เอาล่ะ ข้าจะให้เจ้าเตรียมตัว";
			mes "ที่จะทำลายน้ำแข็งพวกนี้";
			mes "เพื่อช่วยพวกข้าออกไป";
			mes "ถ้าเจ้าพร้อมเมื่อไรก็ให้คุยกับ Jays ละกัน.";
			completequest 15117;
			getexp 100000,100000;
			frozen_daily = 4;
			end;
		}
		if (checkquest(15118,HUNTING) == 2){
			mes .NPCname$;
			mes "โอ้ เจ้ากลับมาแล้ว";
			mes "เจ้าปลอดภัยดีสินะ";
			mes "ข้าเชื่อแล้วว่าเจ้าเก่งจริงๆ.";
			next;
			mes .NPCname2$;
			mes "เอาล่ะ ข้าจะให้เจ้าเตรียมตัว";
			mes "ที่จะทำลายน้ำแข็งพวกนี้";
			mes "เพื่อช่วยพวกข้าออกไป";
			mes "ถ้าเจ้าพร้อมเมื่อไรก็ให้คุยกับ Jays ละกัน.";
			completequest 15118;
			getexp 100000,100000;
			frozen_daily = 4;
			end;
		}
		if (checkquest(15119,HUNTING) == 2){
			mes .NPCname$;
			mes "โอ้ เจ้ากลับมาแล้ว";
			mes "เจ้าปลอดภัยดีสินะ";
			mes "ข้าเชื่อแล้วว่าเจ้าเก่งจริงๆ.";
			next;
			mes .NPCname2$;
			mes "เอาล่ะ ข้าจะให้เจ้าเตรียมตัว";
			mes "ที่จะทำลายน้ำแข็งพวกนี้";
			mes "เพื่อช่วยพวกข้าออกไป";
			mes "ถ้าเจ้าพร้อมเมื่อไรก็ให้คุยกับ Jays ละกัน.";
			completequest 15119;
			getexp 100000,100000;
			frozen_daily = 4;
			end;
		}
	}	
	if (frozen_daily == 4 && checkquest(15112) == -1 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "เจ้าพร้อมแล้วสินะ";
		mes "ข้าก็พร้อมแล้วเหมือนกัน";
		mes "เอาล่ะ ทำลายน้ำแข็งนี้ให้ข้าที.";
		close2;
		classchange 111,"Jays",bc_self;
		monster "ice_d03_i",156,40,"Jays",3798,1,"breakIce::onGCdied";
		setquest 15112;
		end;
	}	
	if (frozen_daily == 5 && checkquest(15113) == -1 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "เยี่ยมเลย มันได้ผล";
		mes "ต่อไปเจ้าก็ช่วย Brides ด้วยสิ.";
		close2;
		classchange 111,"Brides",bc_self;
		monster "ice_d03_i",149,40,"Brides",3797,1,"breakIce::onKNdied";
		setquest 15113;
		end;
	}	
	if (frozen_daily == 6 && checkquest(15116) == -1 && BaseLevel >= 110) {
		mes .NPCname$;
		mes "ยอดเยี่ยมไปเลย";
		mes "เจ้าช่วยพวกข้าไว้จริงๆเลย.";
		next;
		mes .NPCname2$;
		mes "ขอบใจเจ้ามากที่ช่วยเหลือพวกข้า";
		mes "นี่ตือรางวัลสพหรับการช่วยเหลือพวกข้า";
		mes "ขอให้เจ้าโชคดี.";
		next;
		mes .NPCname$;
		mes "แล้วพบกันใหม่";
		setquest 15116;
		frozen_daily = 10;
		getitem 25271,5;
		end;
	}
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.NPCname2$ = "[^FF0000 Brides ^000000]";
		questinfo QTYPE_QUEST, QMARK_YELLOW, "frozen_daily == 0 && checkquest(15110) == -1 && BaseLevel >= 110";
		questinfo QTYPE_QUEST, QMARK_YELLOW, "frozen_daily == 4 && checkquest(15112) == -1 && BaseLevel >= 110";
		questinfo QTYPE_QUEST, QMARK_YELLOW, "frozen_daily == 5 && checkquest(15113) == -1 && BaseLevel >= 110";
		questinfo QTYPE_QUEST, QMARK_YELLOW, "frozen_daily == 6 && checkquest(15116) == -1 && BaseLevel >= 110";
	end;
}

ice_d03_i,153,39,3	script	#DryTwings	4_TRACE,{
	OnInit:
		disablenpc "#DryTwings";
	end;
}
ice_d03_i,153,39,3	script	#Bonefire	4_BONFIRE,{
	OnInit:
		disablenpc "#Bonefire";
	end;
}

-	script	breakIce	-,{
	onGCdied:
		classchange 4_M_BLACKMAN,"Jays",bc_self;
		frozen_daily = 5;
		completequest 15112;
	end;
	onKNdied:
		classchange 4_M_KNIGHT_BLACK,"Brides",bc_self;
		frozen_daily = 6;
		completequest 15113;
	end;
}

ice_d03_i,149,40,6	script	Brides	4_M_FROZEN_KN,{}

-	script	Soil	-1,{
	if((checkquest(15110) == 1) && (countitem(25309) < 10)){
		initnpctimer;
		progressbar "005500", 1;
		specialeffect 18;
		getitem 25309,1;
		disablenpc strnpcinfo(0);
		end;
	}	
	OnTimer300000:	// 300000 = 5 นาที.
	//OnTimer5000:
		stopnpctimer;
		enablenpc strnpcinfo(0);
	end;
}
ice_d03_i,166,35,4	duplicate(Soil)	Soil#1	2372
ice_d03_i,253,57,4	duplicate(Soil)	Soil#2	2372
ice_d03_i,105,118,4	duplicate(Soil)	Soil#3	2372
ice_d03_i,65,219,4	duplicate(Soil)	Soil#4	2372
ice_d03_i,255,208,4	duplicate(Soil)	Soil#5	2372
ice_d03_i,249,256,4	duplicate(Soil)	Soil#6	2372
ice_d03_i,263,28,4	duplicate(Soil)	Soil#7	2372
ice_d03_i,45,56,4	duplicate(Soil)	Soil#8	2372
ice_d03_i,55,241,4	duplicate(Soil)	Soil#9	2372
ice_d03_i,150,267,4	duplicate(Soil)	Soil#10	2372
ice_d03_i,35,172,4	duplicate(Soil)	Soil#11	2372
ice_d03_i,63,148,4	duplicate(Soil)	Soil#12	2372
ice_d03_i,125,80,4	duplicate(Soil)	Soil#13	2372
ice_d03_i,207,91,4	duplicate(Soil)	Soil#14	2372
ice_d03_i,278,169,4	duplicate(Soil)	Soil#15	2372
ice_d03_i,206,207,4	duplicate(Soil)	Soil#16	2372
ice_d03_i,119,205,4	duplicate(Soil)	Soil#17	2372
ice_d03_i,189,105,4	duplicate(Soil)	Soil#18	2372
ice_d03_i,132,34,4	duplicate(Soil)	Soil#19	2372
ice_d03_i,232,227,4	duplicate(Soil)	Soil#20	2372