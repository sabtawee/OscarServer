//===== rAthena Script =======================================
//= Illusion of Moonlight
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

//============================================================
//= mapflag
//============================================================
pay_d03_i	mapflag	nobranch
pay_d03_i	mapflag	nomemo

//============================================================
//= Warp
//============================================================
pay_dun03,140,46,0	script	Shimmering Portal#illusion_moonlight	4_ENERGY_BLUE,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" เข้าสู้ดันเจี้ยน",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1: warp "pay_d03_i",140,44; end;
		case 2: end;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = "~";
	end;
}

pay_d03_i,140,46,0	script	Shimmering Portal#illusion_moonlight_2	4_ENERGY_BLUE,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" ออกจากดันเจี้ยน",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1: warp "pay_dun03",140,44; end;
		case 2: end;
	}
	end;
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = "~";
	end;
}


//============================================================
//= Daily Quest
//============================================================
pay_d03_i,149,45,4	script	Scholar Chunghae#illusion_moonlight	4_F_03,{
	disable_items;
	if (checkquest(.questid[1],PLAYTIME) == 2) { erasequest .questid[0]; erasequest .questid[1]; }
	mes .NPCname$;
	mes "เข้ามาเลยเหล่านักผจญภัย";
	mes "ข้ามีเควสประจำวันให้พวกเจ้าทำ";
	mes "สนใจเควสไหนก็รับไปได้เลย.";
	next;
	switch(select(
		"[^009900"+.sb$+"^000000] หยุดยั้งจอมเวทย์!",
		"[^FF0000"+.sb$+"^000000] ยกเลิก"
	)) {
		case 1:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[0]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[0];
				close;
			}
			else if (isbegin_quest(.questid[0]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[0],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[0],.rewardexp[1];
					getitem .reward[0],.reward[1];
					completequest .questid[0];
					setquest .questid[1];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 2: end;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
		//waitingroom "เควสประจำวัน",0;
		//----------------------------------------
		// base level request.
		//----------------------------------------
		.lvlrequest = 100;
		//----------------------------------------
		// quest reward.
		//----------------------------------------
		setarray .reward, 
			25271, 2;			//Illusion Stone 2 ea.
		setarray .rewardexp,
			2000000, 1000000;	//BEXP ,JEXP for quest id 2000075
		//----------------------------------------
		// quest id.
		//----------------------------------------
		setarray .questid,
			2000075,	//Stop the Wizard! 
			2000076;	//Stop the Wizard!  - Waiting
		//----------------------------------------
		// moster hunt.
		//----------------------------------------
		setarray .qhunt,
			3764, 5;	//Wizard of Truth
		//----------------------------------------
		// quest mark.
		//----------------------------------------
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000075) && BaseLevel >= 100";
		end;
}
pay_d03_i,152,45,4	script	Soldier Junghee#illusion_moonlight	4_M_PAY_SOLDIER,{
	disable_items;
	if (checkquest(.questid[1],PLAYTIME) == 2) { erasequest .questid[0]; erasequest .questid[1]; }
	if (checkquest(.questid[3],PLAYTIME) == 2) { erasequest .questid[2]; erasequest .questid[3]; }
	mes .NPCname$;
	mes "เข้ามาเลยเหล่านักผจญภัย";
	mes "ข้ามีเควสประจำวันให้พวกเจ้าทำ";
	mes "สนใจเควสไหนก็รับไปได้เลย.";
	next;
	switch(select(
		"[^009900"+.sb$+"^000000] รำลึกถึงบรรพบุรุษ!",
		"[^009900"+.sb$+"^000000] ไม่มีความเมตตาต่อภาพลวงตา!",
		"[^FF0000"+.sb$+"^000000] ยกเลิก"
	)) {
		case 1:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[0]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[0];
				close;
			}
			else if (isbegin_quest(.questid[0]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[0],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[0],.rewardexp[1];
					getitem .reward[0],.reward[1];
					completequest .questid[0];
					setquest .questid[1];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 2:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[2]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[2],MOB_NAME)+"^000000 x "+.qhunt[3]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[2];
				close;
			}
			else if (isbegin_quest(.questid[2]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[2],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[2],.rewardexp[3];
					getitem .reward[0],.reward[1];
					completequest .questid[2];
					setquest .questid[3];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 3: end;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
		//waitingroom "เควสประจำวัน",0;
		//----------------------------------------
		// base level request.
		//----------------------------------------
		.lvlrequest = 100;
		//----------------------------------------
		// quest reward.
		//----------------------------------------
		setarray .reward, 
			25271, 3;			//Illusion Stone 3 ea.
		setarray .rewardexp,
			2000000, 1000000,	//BEXP ,JEXP for quest id 2000077
			2000000, 1000000;	//BEXP ,JEXP for quest id 2000079
		//----------------------------------------
		// quest id.
		//----------------------------------------
		setarray .questid,
			2000077,	//Remembering My Ancestors!
			2000078,	//Remembering My Ancestors! - Waiting
			2000079,	//No Mercy for Illusions! - Waiting
			2000080;	//No Mercy for Illusions! - Waiting
		//----------------------------------------
		// moster hunt.
		//----------------------------------------
		setarray .qhunt,
			3763, 20,	//Angry Soldiers
			3759, 20;	//Angry Nine Tail
		//----------------------------------------
		// quest mark.
		//----------------------------------------
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000077) && BaseLevel >= 100";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000079) && BaseLevel >= 100";
		end;
}

//============================================================
//= Mob Spawn
//============================================================
pay_d03_i	monster	Blue Plant	1079,1
pay_d03_i	monster	Green Plant	1080,1,5000,0
pay_d03_i	monster	Yellow Plant	1081,1,5000,0
pay_d03_i	monster	White Plant	1082,5,5000,0
pay_d03_i	monster	Whisper	1185,1,5000,0
pay_d03_i,154,112	monster	Whisper	1185,1,5000,0
pay_d03_i,161,117	monster	Whisper	1185,1,5000,0
pay_d03_i,84,63	monster	Whisper	1185,1,5000,0
pay_d03_i,81,61	monster	Whisper	1185,1,5000,0

pay_d03_i	monster	Resentful Munak	3760,20,5000,0,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Resentful Bongun	3761,20,5000,0,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Resentful Sohee	3762,20,5000,0,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Resentful Soldier	3763,15,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Deranged Adventurer	3765,5,15000,0,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Wizard of the Truth	3764,1,30000,0,"illusion_mob#moonlight::OnKill"
pay_d03_i	monster	Angry Nine Tail	3759,10,5000,0,"illusion_mob#moonlight::OnKill"

-	script	illusion_mob#moonlight	-1,{
	OnInit:
		// ชื่อไฟล์แผนที่.
		.mapname$ = "pay_d03_i";
		// มอนสเตอร์ MVP ID.
		.bossID = 3758;
		// จำนวนมอนสเตอร์ที่ต้องกำจัด เพื่อเรียกบอส.
		.mob_count = 1000;
		$moonlight_boss = 0;	// นับจำนวนบอส หากบอสเกิดจะเป็น 1 และจำนวนมอนสเตอร์ที่ต้องกำจัด จะไม่ถูกนับเพิ่ม. (ป้องกันการสแปมบอส)
								// ถ้าบอสตายจะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้ง.
		$illusion_moonlight = 0;	//จะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้งเมื่อรีสคริป.
		// บอสมอนสเตอร์มีโอกาสเกิดแม้จะกำจัดมอนอื่นๆไม่ครบ
		.lucky_chance = false;	// true = เปิด | false = ปิด
		.max_chance = 10000;	// 100.00%
		.chance = 500;			// 5.00%
		// แสดงการนับจำนวนมอนที่กำจัด
		.show_count = true;	// true = เปิด | false = ปิด
	end;
		
	OnKill:
		if ($moonlight_boss == 1) end;
		$illusion_moonlight++;
		if (.show_count) {
			dispbottom "Illusion of Moonlight: บอสมอนสเตอร์จะปรากฏตัวเมื่อกำจัดมอนสเตอร์ครบ ["+callfunc("F_InsertComma",$illusion_moonlight)+"/"+callfunc("F_InsertComma",.mob_count)+"] ตัว.";
		}
		if ($illusion_moonlight >= .mob_count && $moonlight_boss == 0) {
			donpcevent strnpcinfo(0)+"::OnSummonMVP";
		}
		if (.lucky_chance) {
			if ($illusion_moonlight <= .mob_count) {
				if (rand(.max_chance) <= .chance) {
					donpcevent strnpcinfo(0)+"::OnSummonMVP";
				}
			}
		}
	end;
		
	OnSummonMVP:
		$moonlight_boss++;
		if (.lucky_chance) {
			mapannounce .mapname$,""+getmonsterinfo(.bossID,MOB_NAME)+":  ใครกันที่กล้ามารบกวนการหลับไหลของข้า!!",bc_map,0xFFBF5C,FW_BOLD,20;
			sleep 5000;
		}
		mapannounce .mapname$,"Illusion of Moonlight: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" จะปรากฏตัวในอีก 10 วินาที.",bc_map,0xFFBF5C,FW_BOLD,20;		
		sleep 10000;
		mapannounce .mapname$,"Illusion of Moonlight: บอสมอนสเตอร์ "+getmonsterinfo(.bossID,MOB_NAME)+" ได้ปรากฏตัวขึ้นแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		areamonster .mapname$,0,0,0,0,"--ja--",.bossID,1,strnpcinfo(0)+"::OnMVPDead";	
		$illusion_moonlight = 0;
		end;	
	
	OnMVPDead:
		$moonlight_boss--;
		mapannounce .mapname$,"Illusion of Moonlight: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[0],MOB_NAME)+" ได้ถูกกำจัดลงแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		end;
}


//============================================================
//= Illusion Gears Trader.
//============================================================
pay_d03_i,160,45,4	script	Gemcutter#illusion_moonlight	4_TOWER_17,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" อัพเกรด อาวุธ.",
		""+.sb$+" อัพเกรด ชุดเกราะ.",		
		""+.sb$+" กล่องอัพเกรด."
	)){
		case 1:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "WEAPON_UP_Moonlight",1;
			end;
		case 2:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "ARMOR_UP_Moonlight",1;
			end;
		case 3:
			mes .NPCname$;
			mes "โปรดรอสักครู่.";
			close2;
			callshop "REFINE_Moonlight",1;
			end;
	}
	end;
	
OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";
	.msg$ = "Illusion of Moonlight";
	setunittitle getnpcid(0),.msg$;
	setunitdata getnpcid(0),UNPC_GROUP_ID,1014;
	end;
}
//skycity,63,90,3	duplicate(Gemcutter#illusion_moonlight)	Gemcutter#illusion_moonlight2	4_TOWER_17