//===== rAthena Script =======================================
//= Illusion of Underwater
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

//============================================================
// mapflag
//============================================================
iz_d04_i	mapflag	nobranch
iz_d04_i	mapflag	nomemo

iz_d05_i	mapflag	nobranch
iz_d05_i	mapflag	nomemo

//============================================================
// Illusion Gears Trader.
//============================================================
iz_d04_i,134,228,3	script	Horen#illusion_underwater	4_M_BIBI,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" อัพเกรด อาวุธ.",
		""+.sb$+" อัพเกรด ชุดเกราะ.",
		""+.sb$+" กล่องอัพเกรด."
	)){			
		case 1:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "WEAPON_UP_SEA",1;
			end;
		case 2:
			mes .NPCname$;
			mes "ต่อไปนี้เป็นรายการอุปกรณ์ที่ข้าสามารถจัดการได้.";
			close2;
			callshop "ARMOR_UP_SEA",1;
			end;
		case 3:
			mes .NPCname$;
			mes "โปรดรอสักครู่.";
			close2;
			callshop "REFINE_Underwater",1;
			end;
	}
	end;
	
OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";		
	.msg$ = "Illusion of Underwater";
	setunittitle getnpcid(0),.msg$;
	setunitdata getnpcid(0),UNPC_GROUP_ID,1014;
	end;
}
//skycity,92,87,3	duplicate(Horen#illusion_underwater)	Horen#illusion_underwater2	4_M_BIBI


//============================================================
// Mob Spawn
//============================================================
iz_d04_i,0,0	monster	Deep Sea Sropho	20801,45,5000,0,"illusion_mob#Underwater::OnF4Kill"
iz_d04_i,0,0	monster	Deep Sea Obeaune	20802,45,5000,0,"illusion_mob#Underwater::OnF4Kill"
iz_d04_i,0,0	monster	Deep Sea Deviace	20803,45,5000,0,"illusion_mob#Underwater::OnF4Kill"
iz_d04_i,0,0	monster	Deep Sea Marse	20804,45,5000,0,"illusion_mob#Underwater::OnF4Kill"
iz_d04_i,0,0	monster	Deep Sea Merman	20805,45,5000,0,"illusion_mob#Underwater::OnF4Kill"

iz_d05_i,0,0	monster	Deep Sea Sedora	20806,45,5000,0,"illusion_mob#Underwater::OnF5Kill"
iz_d05_i,0,0	monster	Deep Sea Swordfish	20807,45,5000,0,"illusion_mob#Underwater::OnF5Kill"
iz_d05_i,0,0	monster	Deep Sea Strouf	20808,45,5000,0,"illusion_mob#Underwater::OnF5Kill"
iz_d05_i,0,0	monster	Deep Sea Phen	20809,45,5000,0,"illusion_mob#Underwater::OnF5Kill"
iz_d05_i,0,0	monster	Deep Sea King Dramoh	20810,45,5000,0,"illusion_mob#Underwater::OnF5Kill"
iz_d05_i,0,0	monster	Deep Sea Witch	20843,1,5000,0,"illusion_mob#Underwater::OnF5Kill"
-	script	illusion_mob#Underwater	-1,{
	OnInit:
		// ชื่อไฟล์แผนที่.
		.map_F4$ = "iz_d04_i";
		.map_F5$ = "iz_d05_i";
		// มอนสเตอร์ MVP ID.
		setarray .bossID, 20811,20843;
		// จำนวนมอนสเตอร์ที่ต้องกำจัด เพื่อเรียกบอส.
		.mob_count = 2000;
		$kraken_boss = 0;
		$witchz_boss = 0;	// นับจำนวนบอส หากบอสเกิดจะเป็น 1 และจำนวนมอนสเตอร์ที่ต้องกำจัด จะไม่ถูกนับเพิ่ม. (ป้องกันการสแปมบอส)
							// ถ้าบอสตายจะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้ง.
		$illusion_kraken = 0;	//จะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้งเมื่อรีสคริป.
		$illusion_witchz = 0;	//จะนับจำนวนมอนสเตอร์ที่ต้องกำจัดใหม่อีกครั้งเมื่อรีสคริป.
		// บอสมอนสเตอร์มีโอกาสเกิดแม้จะกำจัดมอนอื่นๆไม่ครบ
		.lucky_chance = false;	// true = เปิด | false = ปิด
		.max_chance = 10000;	// 100.00%
		.chance = 500;			// 5.00%
		// แสดงการนับจำนวนมอนที่กำจัด
		.show_count = true;	// true = เปิด | false = ปิด
		end;
		
	OnF4Kill:
		if ($kraken_boss == 1) end;
		$illusion_kraken++;
		if (.show_count) {
			dispbottom "Illusion of Underwater: บอสมอนสเตอร์จะปรากฏตัวเมื่อกำจัดมอนสเตอร์ครบ ["+callfunc("F_InsertComma",$illusion_kraken)+"/"+callfunc("F_InsertComma",.mob_count)+"] ตัว.";
		}
		if ($illusion_kraken >= .mob_count && $kraken_boss == 0) {
			donpcevent strnpcinfo(0)+"::OnF4MVP";
		}
		if (.lucky_chance) {
			if ($illusion_kraken <= .mob_count) {
				if (rand(.max_chance) <= .chance) {
					donpcevent strnpcinfo(0)+"::OnF4MVP";
				}
			}
		}
	end;
		
	OnF5Kill:
		if ($witchz_boss == 1) end;
		$illusion_witchz++;
		if (.show_count) {
			dispbottom "Illusion of Underwater: บอสมอนสเตอร์จะปรากฏตัวเมื่อกำจัดมอนสเตอร์ครบ ["+callfunc("F_InsertComma",$illusion_witchz)+"/"+callfunc("F_InsertComma",.mob_count)+"] ตัว.";
		}
		if ($illusion_witchz >= .mob_count && $witchz_boss == 0) {
			donpcevent strnpcinfo(0)+"::OnF5MVP";
		}
		if (.lucky_chance) {
			if ($illusion_witchz <= .mob_count) {
				if (rand(.max_chance) <= .chance) {
					donpcevent strnpcinfo(0)+"::OnF5MVP";
				}
			}
		}
	end;
	
	OnF4MVP:
		$kraken_boss++;
		if (.lucky_chance) {
			mapannounce .map_F4$,""+getmonsterinfo(.bossID[0],MOB_NAME)+":  ใครกันที่กล้ามารบกวนการหลับไหลของข้า!!",bc_map,0xFFBF5C,FW_BOLD,20;
			sleep 5000;
		}
		mapannounce .map_F4$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[0],MOB_NAME)+" จะปรากฏตัวในอีก 10 วินาที.",bc_map,0xFFBF5C,FW_BOLD,20;		
		sleep 10000;
		mapannounce .map_F4$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[0],MOB_NAME)+" ได้ปรากฏตัวขึ้นแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		monster .map_F4$,0,0,"--ja--",.bossID[0],1,strnpcinfo(0)+"::OnMVPF4Dead";	
		$illusion_kraken = 0;
		end;
	
	OnF5MVP:
		$witchz_boss++;
		if (.lucky_chance) {
			mapannounce .map_F5$,""+getmonsterinfo(.bossID[1],MOB_NAME)+":  ใครกันที่กล้ามารบกวนการหลับไหลของข้า!!",bc_map,0xFFBF5C,FW_BOLD,20;
			sleep 5000;
		}
		mapannounce .map_F5$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[1],MOB_NAME)+" จะปรากฏตัวในอีก 10 วินาที.",bc_map,0xFFBF5C,FW_BOLD,20;		
		sleep 10000;
		mapannounce .map_F5$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[1],MOB_NAME)+" ได้ปรากฏตัวขึ้นแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		monster .map_F5$,0,0,"--ja--",.bossID[1],1,strnpcinfo(0)+"::OnMVPF5Dead";	
		$illusion_witchz = 0;
		end;
		
	OnMVPF4Dead:
		mapannounce .map_F4$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[0],MOB_NAME)+" ได้ถูกกำจัดลงแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		$kraken_boss--;
		end;
		
	OnMVPF5Dead:
		mapannounce .map_F5$,"Illusion of Underwater: บอสมอนสเตอร์ "+getmonsterinfo(.bossID[1],MOB_NAME)+" ได้ถูกกำจัดลงแล้ว !!",bc_map,0xFFBF5C,FW_BOLD,20;
		$illusion_witchz--;
		end;	
}


//============================================================
// Daily Quest.
//============================================================
iz_d04_i,126,227,5	script	Jerry#ill_underw	1_M_04,{
	disable_items;
	if (checkquest(.questid[1],PLAYTIME) == 2) { erasequest .questid[0]; erasequest .questid[1]; }
	if (checkquest(.questid[3],PLAYTIME) == 2) { erasequest .questid[2]; erasequest .questid[3]; }
	if (checkquest(.questid[5],PLAYTIME) == 2) { erasequest .questid[4]; erasequest .questid[5]; }
	if (checkquest(.questid[7],PLAYTIME) == 2) { erasequest .questid[6]; erasequest .questid[7]; }
	mes .NPCname$;
	mes "เข้ามาเลยเหล่านักผจญภัย";
	mes "ข้ามีเควสประจำวันให้พวกเจ้าทำ";
	mes "สนใจเควสไหนก็รับไปได้เลย.";
	next;
	switch(select(
		"[^009900"+.sb$+"^000000] รวบรวม Abyssal Essences",
		"[^009900"+.sb$+"^000000] กำจัดมอนสเตอร์ที่กำหนด",
		"[^009900"+.sb$+"^000000] กำจัดมอนสเตอร์ที่กำหนด (1)",
		"[^009900"+.sb$+"^000000] กำจัดมอนสเตอร์ที่กำหนด (2)",
		"[^FF0000"+.sb$+"^000000] ยกเลิก"
	)) {
		case 1:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[0]) == 0) {
				mes .NPCname$;
				mes "ข้าต้องการ <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM> x "+.qresource[1]+" อัน";
				mes "เจ้าช่วยหามันมาให้ข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "เจ้าสามารถหา <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM>";
				mes "จากมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[0];
				close;
			}
			else if (isbegin_quest(.questid[0]) == 1) {
				mes .NPCname$;
				if (countitem(.qresource[0]) >= .qresource[1]) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					delitem .qresource[0],.qresource[1];
					getexp .rewardexp[0],.rewardexp[1];
					getitem .reward[0],.reward[1];
					completequest .questid[0];
					setquest .questid[1];
					close;
				}
				mes "เจ้าสามารถหา <ITEM>"+getitemname(.qresource[0])+"<INFO>"+.qresource[0]+"</INFO></ITEM>";
				mes "จากมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา";
				mes "เมื่อหาครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 2:
			if (BaseLevel < .lvlrequest) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[2]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[2],MOB_NAME)+"^000000 x "+.qhunt[3]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[4],MOB_NAME)+"^000000 x "+.qhunt[5]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[2];
				close;
			}
			else if (isbegin_quest(.questid[2]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[2],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[2],.rewardexp[3];
					getitem .reward[0],.reward[1];
					completequest .questid[2];
					setquest .questid[3];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 3:
			if (BaseLevel < .lvlrequest[1]) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest[1]+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[4]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[0],MOB_NAME)+"^000000 x "+.qhunt[1]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[2],MOB_NAME)+"^000000 x "+.qhunt[3]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[4],MOB_NAME)+"^000000 x "+.qhunt[5]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[4];
				close;
			}
			else if (isbegin_quest(.questid[4]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[4],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[4],.rewardexp[5];
					getitem .reward[0],.reward[1]+1;
					completequest .questid[4];
					setquest .questid[5];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}			
		case 4:
			if (BaseLevel < .lvlrequest[1]) {
				mes .NPCname$;
				mes "เจ้าต้องมีเลเวลอย่างน้อย ^990099"+.lvlrequest[1]+"^000000";
				mes "จึงจะสามารถรับเควสได้.";
				close;
			}
			if (isbegin_quest(.questid[6]) == 0) {
				mes .NPCname$;
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[6],MOB_NAME)+"^000000 x "+.qhunt[7]+" ตัว";
				mes "กำจัด ^990099"+getmonsterinfo(.qhunt[8],MOB_NAME)+"^000000 x "+.qhunt[9]+" ตัว";
				mes "เจ้าช่วยข้าได้ไหม?";
				next;
				if(select("[^009900"+.sb$+"^000000] ยอมรับเควส :[^FF0000"+.sb$+"^000000] ยกเลิก") == 2){
					mes .NPCname$;
					mes "แล้วพบกันใหม่.";
					close;
				}
				mes .NPCname$;
				mes "ขอบคุณเจ้ามาก";
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				setquest .questid[6];
				close;
			}
			else if (isbegin_quest(.questid[6]) == 1) {
				mes .NPCname$;
				if (checkquest(.questid[6],HUNTING) == 2) {
					mes "ขอบคุณเจ้ามาก";
					mes "นี่คือของรางวัลของเจ้า.";
					getexp .rewardexp[6],.rewardexp[7];
					getitem .reward[0],.reward[1]+1;
					completequest .questid[6];
					setquest .questid[7];
					close;
				}
				mes "อย่าลืมกำจัดมอนเสตอร์ที่อยู่ภายในดันเจี้ยนลวงตา.";
				mes "เมื่อครบแล้วให้กลับมาหาข้าอีกครั้ง.";
				close;
			}
			else {
				mes .NPCname$;
				mes "วันนี้เจ้าได้ทำเควสนี้ไปแล้ว";
				mes "โปรดกลับมาใหม่ในวันพรุ่งนี้.";
				close;
			}
		case 5: end;
	}
	end;
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
		//waitingroom "เควสประจำวัน",0;
		//----------------------------------------
		// base level request.
		//----------------------------------------
		setarray .lvlrequest, 140, 180;
		//----------------------------------------
		// quest reward.
		//----------------------------------------
		setarray .reward, 
			25271, 3;			//Illusion Stone 3 ea.
		setarray .rewardexp,
			2000000, 1000000,	//BEXP ,JEXP for quest id 2000009
			4000000, 2000000,	//BEXP ,JEXP for quest id 2000011
			4000000, 2000000,	//BEXP ,JEXP for quest id 2000013
			4000000, 2000000;	//BEXP ,JEXP for quest id 2000015	
		//----------------------------------------
		// quest id.
		//----------------------------------------
		setarray .questid,
			2000009,	//Abyssal Essence Collection
			2000010,	//Abyssal Essence Collection - Waiting
			2000011,	//Deep Sea Creatures Control
			2000012,	//Deep Sea Creatures Control - Waiting
			2000013,	//Deep Sea Creatures Control (1)
			2000014,	//Deep Sea Creatures Control (1) - Waiting
			2000015,	//Deep Sea Creatures Control (2)
			2000016;	//Deep Sea Creatures Control (2) - Waiting
		//----------------------------------------
		// quest request.
		//----------------------------------------
		setarray .qresource,
			25899, 10;		//Abyssal Essence x10
		//----------------------------------------
		// moster hunt.
		//----------------------------------------
		setarray .qhunt,
			20801, 10,	//Deep Sea Sropho x10
			20803, 10,	//Deep Sea Deviace x10
			20804, 10,	//Deep Sea Marse x10
			20806, 15,	//Deep Sea Sedora x15
			20808, 15;	//Deep Sea Strouf x15
		//----------------------------------------
		// quest mark.
		//----------------------------------------
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000009) && BaseLevel >= 140";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000011) && BaseLevel >= 140";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000013) && BaseLevel >= 180";
		questinfo QTYPE_DAILYQUEST, QMARK_YELLOW, "!isbegin_quest(2000015) && BaseLevel >= 180";
		end;
}

//============================================================
// Warp to 2F.
//============================================================
iz_d04_i,124,225,5	script	Guard#ill_underw	4_M_KY_SOLD,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" ไปชั้นถัดไป",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1:
			mes .NPCname$;
			mes "ขอให้โชคดี.";
			close2;
			warp "iz_d05_i",141,188;
			end;
			
		case 2:
			mes .NPCname$;
			mes "แล้วพบกันใหม่.";
			close;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
	end;
}

//============================================================
// Warp to 1F.
//============================================================
iz_d05_i,138,187,5	script	Guard#ill_underw2	4_M_KY_SOLD,{
	mes .NPCname$;
	mes "มีอะไรให้ข้าช่วยไหม?";
	next;
	switch(select(
		""+.sb$+" กลับไปชั้นก่อนหน้านี้",
		""+.sb$+" ไม่มีอะไร"
	)) {
		case 1:
			mes .NPCname$;
			mes "ขอให้โชคดี.";
			close2;
			warp "iz_d04_i",129,229;
			end;
			
		case 2:
			mes .NPCname$;
			mes "แล้วพบกันใหม่.";
			close;
	}
	
	OnInit:
		.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
		.sb$ = ">";
	end;
}